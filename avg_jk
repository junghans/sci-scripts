#! /usr/bin/awk -f
BEGIN{
	#Version check, use gawk only !
	"awk --version" | getline version;
	if ( version !~ /GNU Awk/)
	{
		print "#WARING: This is not gawk (GNU awk). Could cause problem!";
	}
	print "#This is avg_jk 0.98 (10.01.07)";
	if (ARGC>2)
	{
		print "#WARING: Average over multiple files!!";
	}
	if (name=="")
	{
		if (ARGV[1]=="")
		{
			print "#FILE: STDIN";
		}
		else
		{
			print "#FILE:",ARGV[1];
		}
	}
	else
	{
		print "#FILE:",name,"(set by user)";
	}	
	if (col=="")
	{
		col=1;
		print "#Setting column to",col,"(use option \"-v col=X\" to change)";
	}
	else
	{
		print "#Using column",col,"(set by user)";
	}
	if (N_B=="")
	{
		N_B=16;
		print "#Setting Number of blocks to",N_B,"(use option \"-v N_B=X\" to change)";
	}
	else
	{
		print "#Using ",N_B," blocks ";
	}
	c=0;
	warn=0;
}

/^[@#]/{
	next;
}

{
	if (NF>=col)
	{
		x[c]=$col;
		c++;
	}
	else
	{
		if (warn == 0)
		{
			print "# Not enough data in line",NR;
			warn++;
		}
	}	
	
}

END{
	L_B=int(c/N_B);
	sum=0;
	sum2=0;
	for (b=0;b<N_B;b++){
		for (i=0;i<L_B;i++){
			nr=int(b*L_B+i)
			sum+=x[nr];
			sum2+=x[nr]*x[nr];
			for (b2=0;b2<N_B;b2++){
				if (b2!=b){
					sum_b[b2]+=x[nr];
				}
			}
		}
	}	
	sum=sum/(L_B*N_B);
	sum2=sum2/(L_B*N_B);
	se_n=(sum2-sum*sum)/(L_B*N_B);
	se_n=sqrt(se_n);
	se_b=0;
	for (b=0;b<N_B;b++){
		sum_b[b]=sum_b[b]/(L_B*(N_B-1));
		#print b,sum,sum_b[b],se;
		se_b+=(sum_b[b]-sum)*(sum_b[b]-sum);
	}
#	print se;
	se_b=se_b*(N_B-1)/N_B;
#	print se;
	se_b=sqrt(se_b);
	print "#Output format: avg se naiv_se values_jk values";
	print sum,se_b,se_n,L_B*N_B,c;
}
#fs md5sum 83e6f5f92e44f4ea03a76a8cd04f1b06
